buildscript {
	dependencies {
		classpath "org.liquibase:liquibase-core:4.33.0"
	}
}

plugins {
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jetbrains.kotlin.jvm' version '2.2.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
	id "org.liquibase.gradle" version "3.1.0"
	id "com.google.osdetector" version "1.7.3"
	id 'groovy'
}

group = 'com.softeno'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', '2025.0.1')
	set('testcontainersVersion', '2.0.3')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework:spring-jdbc'
	runtimeOnly 'org.postgresql:r2dbc-postgresql'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-slf4j'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// test utils
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'io.mockk:mockk:1.14.9'
	testImplementation 'com.ninja-squad:springmockk:4.0.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// wiremock
	testImplementation 'org.wiremock:wiremock-standalone:3.13.2'

	// monitoring
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// elk
	implementation 'net.logstash.logback:logstash-logback-encoder:8.1'

	// testcontainers
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:r2dbc'

	// liquibase
	implementation ('org.liquibase:liquibase-core:4.33.0') {
		exclude group: 'javax.xml.bind', module: 'jaxb-receiver'
		exclude group: 'org.liquibase.ext', module: 'liquibase-hibernate5'
	}
	liquibaseRuntime 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	liquibaseRuntime 'org.jetbrains.kotlin:kotlin-reflect'
	liquibaseRuntime 'org.liquibase.ext:liquibase-postgresql:4.33.0'
	liquibaseRuntime 'org.postgresql:postgresql:42.7.10'
	liquibaseRuntime 'ch.qos.logback:logback-core:1.5.30'
	liquibaseRuntime 'ch.qos.logback:logback-classic:1.5.30'
	liquibaseRuntime 'info.picocli:picocli:4.7.7'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.5.10'
	liquibaseRuntime sourceSets.main.output

	// spring security
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// kafka
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'io.projectreactor.kafka:reactor-kafka'
	testImplementation 'org.springframework.kafka:spring-kafka-test'

	// circuit breaker
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'

	// macOs ARM only
	if (osdetector.classifier == "osx-aarch_64") {
		runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.2.3.Final:${osdetector.classifier}")
	}

	// springdoc
	implementation 'org.springdoc:springdoc-openapi-starter-common:2.8.15'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.15'

	// zipkin
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'
	implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'
	implementation 'io.zipkin.reporter2:zipkin-sender-urlconnection'

}

liquibase {
	activities {
		main {
			changelogFile "src/main/resources/db/db.changelog.yml"
			url "jdbc:postgresql://localhost:5432/application"
			referenceUrl "jdbc:postgresql://localhost:5432/application"
			referenceUsername "admin"
			referencePassword "admin"
			username "admin"
			password "admin"
			driver 'org.postgresql.Driver'
			difftypes 'data'
			logLevel "info"
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
